set term png size 800,1200
set output "GCMC-mixed.png"

set multiplot layout 1,2

# Define plot style 'stacked histogram'
# with additional settings
set style data histogram
set style histogram rowstacked
set style fill solid border -1
set boxwidth 0.9

set style line 1 lt 1 lc "black" lw 3 pt 5 ps 1.5
set style line 2 lt 1 lc "red" lw 3 pt 5 ps 1.5
set style line 81 lt 0 lc rgb "#808080" lw 0.5

#location of legend. Turn off by `set nokey`
#set nokey 
set key font ", 20" left top

set ylabel "milisecond" font ", 20" offset char 0.75
set y2label "% Acceptance" font ", 20" offset char -0.75
set xlabel "CD-CBMC parameters (position:orientation)" font ", 20" offset char 0,-1

#set y1tics 4
set xtics rotate by 45 right
set xtics nomirror
set ytics nomirror
#set grid ytics back ls 81
set border 4095 back ls 1 
set tics font ", 16"

set y2tics 0.1
set y2range  [0:0.6]
#set grid y2tics back ls 81

set size 1.0, 0.5
set origin 0.0, 0.0

set label 1 "30K atoms" at graph 0.02,0.7 font ", 20"
plot '010k-transfer-profile.dat' using ($2*1.0E-6) axis x1y1 t "CPU" lc rgb "0x85c9f2", \
    '' using ($3*1.0E-6):xtic(1) axis x1y1 t "GPU" lc "red" , \
    "010k-transfer-acceptance.dat" u 2 w lp ls 1 axis x1y2 t "Acc"


set size 1.0, 0.5
set origin 0.0, 0.5

set label 1 "300K atoms" at graph 0.02,0.7 font ", 20"
plot '100k-transfer-profile.dat' using ($2*1.0E-6) t "CPU" lc rgb "0x85c9f2", \
    '' using ($3*1.0E-6):xtic(1) t "GPU" lc "red" , \
    "100k-transfer-acceptance.dat" u 2 w lp ls 1 axis x1y2 t "Acc"